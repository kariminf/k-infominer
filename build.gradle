/*
 * Gradle build specifications for
 * KInfoMiner: Kariminf Information miner
 * 
 * Copyright 2017 Abdelkrime Aries <kariminfo0@gmail.com>
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'jacoco'

// Gradle java plugin
sourceCompatibility = 1.8
targetCompatibility = 1.8

// Release version
version = '1.0.0'

ext.current_Date = getDate()


// Jar creation
jar {
	from {
            configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
        }
    manifest {
        attributes 'Created-By': System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')',
                   "Specification-Title"    : "Kariminf Information Miner",
                   "Specification-Version"  : version,
                   "Specification-Vendor"   : "Kariminf",
                   "Implementation-Title"   : "kariminf.kinfominer",
                   "Implementation-Version" : version + "-build" + current_Date ,
                   "Implementation-Vendor"  : "Kariminf",
                   "Built-With": "gradle-${project.getGradle().getGradleVersion()}, groovy-${GroovySystem.getVersion()}",
                   "Build-Time": current_Date,
                   "Built-By": project.hasProperty('BF_USER') ? project.getProperty('BF_USER') : System.getProperty('user.name'),
                   "Built-On": "${InetAddress.localHost.hostName}/${InetAddress.localHost.hostAddress}"
    }
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmm')
    return formattedDate
}
 
repositories {
    jcenter()
}

configurations {
    // configuration that holds jars to include in the jar
    extraLibs
}

dependencies {
    // The production code uses the SLF4J logging API at compile time
    compile 'org.slf4j:slf4j-api:1.7.21'
    
    extraLibs files('lib/sqlitejdbc-v056.jar')

    testCompile 'junit:junit:4.12'
    
    configurations.compile.extendsFrom(configurations.extraLibs)
}

javadoc {    
    source = sourceSets.main.allJava
    options.memberLevel = JavadocMemberLevel.PRIVATE
    //classpath = configurations.compile
    destinationDir = file("docs")
    failOnError false
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport
